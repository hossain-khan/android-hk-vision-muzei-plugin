plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'org.jmailen.kotlinter'

    // KSP plugin
    // https://github.com/google/ksp/releases
    id("com.google.devtools.ksp").version("2.1.21-2.0.1")
}


// Creates a variable called keystorePropertiesFile, and initializes it to the keystore.properties file.
// https://developer.android.com/studio/build/gradle-tips#remove-private-signing-information-from-your-project
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Loads the keystore.properties file into the keystoreProperties object.
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    // For CI builds, use debug cert to pass it.
    // https://developer.android.com/studio/publish/app-signing#debug-mode
    // https://coderwall.com/p/r09hoq/android-generate-release-debug-keystores
    keystoreProperties['keyAlias'] = 'androiddebugkey'
    keystoreProperties['keyPassword'] = 'android'
    keystoreProperties['storeFile'] = '../keystore/debug.keystore'
    keystoreProperties['storePassword'] = 'android'
}

java {
    toolchain {
        // Configures the Java toolchain to use Java 17
        // https://docs.gradle.org/current/userguide/toolchains.html
        languageVersion = JavaLanguageVersion.of(17)
    }
}

android {
    compileSdk 36

    defaultConfig {
        applicationId "com.hossainkhan.vision"
        minSdkVersion 21
        targetSdkVersion 36 // NOTE: On update `enable_launcher` should be added for target API

        // Update the version code and version name before each release
        // Current release:
        // - https://github.com/hossain-khan/android-hk-vision-muzei-plugin/releases
        versionCode 15
        versionName "1.12"

        // The authority for the `MuzeiArtProvider` and `MuzeiArtDocumentsProvider`
        def hkVisionAuthorityValue = "com.hossainkhan.vision"
        manifestPlaceholders = [hkVisionAuthority: hkVisionAuthorityValue]
        buildConfigField "String", "HK_VISION_MUZEI_PROVIDER_AUTHORITY", "\"${hkVisionAuthorityValue}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    namespace 'com.hossainkhan.vision'

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    // https://developer.android.com/jetpack/androidx/releases/activity
    implementation "androidx.activity:activity-ktx:1.10.1"
    // https://developer.android.com/jetpack/androidx/releases/fragment
    implementation "androidx.fragment:fragment-ktx:1.8.8"

    // Muzei Plugin
    // https://github.com/romannurik/muzei/releases
    implementation "com.google.android.apps.muzei:muzei-api:3.4.2"

    // AndroidX Work Manager
    // https://developer.android.com/jetpack/androidx/releases/work
    implementation "androidx.work:work-runtime-ktx:2.10.2"

    // Retrofit for web API access
    // https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:3.0.0"
    // JSON Converter -  https://github.com/square/moshi/
    implementation "com.squareup.retrofit2:converter-moshi:3.0.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.15.2"
    //implementation "com.squareup.moshi:moshi-kotlin-codegen:1.15.1"
    ksp("com.squareup.moshi:moshi-kotlin-codegen:1.15.2")

    // Firebase Google Analytics
    // https://firebase.google.com/docs/analytics/get-started?platform=android
    //implementation "com.google.firebase:firebase-analytics:17.4.2"
    //implementation "com.google.firebase:firebase-analytics-ktx:17.4.2"

    // Add the Firebase Crashlytics SDK.
    // https://firebase.google.com/docs/crashlytics/get-started?platform=android
    implementation "com.google.firebase:firebase-crashlytics:19.4.4"

    //
    // Unit tests and related libraries
    //
    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.14.4"
}
